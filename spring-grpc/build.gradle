plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.8' apply false
    id 'io.spring.dependency-management' version '1.1.5'
    id 'com.google.protobuf' version '0.9.4' apply false
}

allprojects {
    group = 'com.springstudy'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    ext {
        grpcVersion = '1.58.1'
        protobufVersion = '3.25.1'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.google.protobuf'

    dependencies {
        implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
        implementation "io.grpc:grpc-protobuf:$grpcVersion"
        implementation "io.grpc:grpc-stub:$grpcVersion"
        implementation "com.google.protobuf:protobuf-java:$protobufVersion"
        implementation 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:${protobufVersion}"
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java {}
                }
                task.plugins {
                    grpc {}
                }
            }
        }
    }

    compileJava {
        dependsOn generateProto
        sourceSets {
            main {
                java {
                    srcDirs 'src/main', 'build/generated/source/proto/main/java', 'build/generated/source/proto/main/grpc'
                }
            }
        }
    }

    tasks.withType(JavaCompile) {
        dependsOn tasks.withType(com.google.protobuf.gradle.GenerateProtoTask)
    }

    test {
        useJUnitPlatform()
    }
}

project(':grpc-interface') {
    dependencies {
        compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    }
}

project(':grpc-client') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation project(':grpc-interface')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'net.devh:grpc-client-spring-boot-starter:2.15.0.RELEASE'
        implementation 'org.springframework.boot:spring-boot-starter'
    }

    bootJar.enabled = true
    jar.enabled = false
}

project(':grpc-server') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation project(':grpc-interface')
        implementation 'net.devh:grpc-server-spring-boot-starter:2.15.0.RELEASE'
        implementation 'org.springframework.boot:spring-boot-starter'
    }

    bootJar.enabled = true
    jar.enabled = false
}


project(':rest-api-server') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter'
    }

    bootJar.enabled = true
    jar.enabled = false
}
